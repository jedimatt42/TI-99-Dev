## BLOCK: 1
( TITLE SCREEN )
: .AT ( X,Y -- ) SWAP 32 * + CURPOS ! ;
: ALLCOLOR ( F,B -- ) DUP SCREEN
15 0 DO OVER OVER I COLOR LOOP DROP DROP ;
: TITLE
GRAPHICS
1 13 ALLCOLOR
5 7 .AT ." FORMULA-TI RACING"
9 8 .AT ." MATTHEW SPLETT"
10 11 .AT ." 1984,2015"
20 6 .AT ." PRESS FIRE TO BEGIN" ;
: WAITFIRE BEGIN 1 JCRU 1 = UNTIL ;

( LOAD OTHER BLOCKS )
: LOADALL 7 2 DO I LOAD LOOP ;
LOADALL

## BLOCK: 2
( CAR PATTERNS)
BASE->R HEX
: RACECAR
03C7 CFFF CFCC 0B0B 68 SPCHAR
0B0B 0FCF CFFF C7C4 69 SPCHAR
C0E3 F3FF F333 D0D0 6A SPCHAR
D0D0 F0F3 F3FF E323 6B SPCHAR
;
: CURBS
FBF5 FBF5 FBF5 FBF5 6C CHAR
DFAF DFAF DFAF DFAF 6D CHAR
;



R->BASE

## BLOCK: 3
( DRAW TRACK )
: DRAWTRACK
CLS CURBS
1 15 ALLCOLOR
5 0 24 108 VCHAR
25 0 24 109 VCHAR ;











## BLOCK: 4
( ADD SPRITES )
: SPRSETUP DELALL 2 MAGNIFY ;
: DRIVER 121 160 13 104 1 SPRITE ;
: RNDSPEED ( --n ) 10 RND 3 + ;
: ENEMIES
9 2 DO
  17 I 20 * + 9 I 3 + 104 I SPRITE
  0 RNDSPEED I MOTION
LOOP 9 #MOTION ;

: GOCARS RANDOMIZE SPRSETUP RACECAR DRIVER ENEMIES ;






## BLOCK: 5
( CONTROL LOOP )
5 VARIABLE LIVES
: SHOWLIVES 2 2 .AT LIVES @ . ;
: CRASH -1 LIVES +! SHOWLIVES ;
: CONTROL
0 JMODE !
5 LIVES !
SHOWLIVES
BEGIN
  COINCALL IF CRASH ENDIF
LIVES @ 1 < UNTIL
;





## BLOCK: 6
( LAUNCH GAME )
: DEVMODE 0 #MOTION TEXT ;
: GAME
  TITLE WAITFIRE
  DRAWTRACK
  GOCARS
  CONTROL
  DEVMODE
;
." Enter GAME to begin."







## BLOCK: 7
















