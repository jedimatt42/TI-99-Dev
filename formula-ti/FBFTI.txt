## BLOCK: 1
( TITLE SCREEN )
: ALLCOLOR ( F B -- ) DUP SCREEN
15 0 DO OVER OVER I COLOR LOOP DROP DROP ;
: TITLE
GRAPHICS
1 13 ALLCOLOR
7 5 GOTOXY ." FORMULA-TI RACING"
8 9 GOTOXY ." MATTHEW SPLETT"
11 10 GOTOXY ." 1984,2015"
6 20 GOTOXY ." PRESS FIRE TO BEGIN" ;
: WAITFIRE BEGIN 1 JOYST DROP DROP 18 = UNTIL ;
-->





## BLOCK: 2
( CAR PATTERNS)
BASE->R HEX
: ZERO
0000 0000 0000 0000 00 SPCHAR
0000 0000 0000 0000 01 SPCHAR
0000 0000 0000 0000 02 SPCHAR
0000 0000 0000 0000 03 SPCHAR ;
: RACECAR
03C7 CFFF CFCC 0B0B 04 SPCHAR
0B0B 0FCF CFFF C7C4 05 SPCHAR
C0E3 F3FF F333 D0D0 06 SPCHAR
D0D0 F0F3 F3FF E323 07 SPCHAR ;
: CURBS
FBF5 FBF5 FBF5 FBF5 6C CHAR
DFAF DFAF DFAF DFAF 6D CHAR ;
R->BASE -->

## BLOCK: 3
( DRAW TRACK )
: SPRSETUP
GRAPHICS DELALL 2 MAGNIFY ZERO ;
: DRAWTRACK
SPRSETUP CLS CURBS
1 15 ALLCOLOR
5 0 24 108 VCHAR
25 0 24 109 VCHAR ;
-->








## BLOCK: 4
( ADD SPRITES )
: DRIVER 121 160 13 4 0 SPRITE ;
: RNDSPEED ( -- N ) 10 RND 3 + ;
: ENEMIES
8 1 DO
  I 20 * 37 + 9 I 3 + 4 I SPRITE
  0 RNDSPEED I MOTION
LOOP
8 #MOTION ;
: GOCARS RANDOMIZE RACECAR DRIVER ENEMIES ;
: CRASHANIM
400 1 DO
  I 2 MOD IF 0 0 SPRCOL ELSE 6 0 SPRCOL ENDIF
LOOP ;
-->


## BLOCK: 5
( GAME RULES )
5 VARIABLE LIVES
: SHOWLIVES 2 2 GOTOXY LIVES @ . ;
: CRASH -1 LIVES +! SHOWLIVES CRASHANIM DRIVER ENEMIES ;
: GETJX ( -- X ) 1 JOYST DROP SWAP DROP ;
: DRIVERX ( -- X ) 0 SPRGET DROP ;
: STOPCAR 0 0 0 MOTION ;
: STEER ( JX -- MX )
DUP 252 = IF DROP 244 ELSE
DUP 4 = IF DROP 12 ENDIF ENDIF ;
: TRACKLIMITS
GETJX DUP 252 = DRIVERX 50 < AND IF STOPCAR DROP ELSE
      DUP 4 = DRIVERX 180 > AND IF STOPCAR DROP ELSE
      STEER 0 0 MOTION
        ENDIF ENDIF ;
-->

## BLOCK: 6
( CONTROL LOOP )
: CONTROL
5 LIVES ! SHOWLIVES
BEGIN COINCALL IF CRASH ENDIF
  TRACKLIMITS
LIVES @ 0= UNTIL
DELALL ;
-->









## BLOCK: 7
( LAUNCH GAME )
: DEVMODE 0 #MOTION TEXT ;
: GAME
  TITLE WAITFIRE
  DRAWTRACK
  GOCARS
  CONTROL
;
CR ." Enter GAME to begin."








## BLOCK: 8
















